-*- mode: m4 -*-
AC_PREREQ(2.52)
AC_INIT(libglade, 1.99,
        [http://bugzilla.gnome.org/enter_bug.cgi?product=libglade])

AC_CONFIG_SRCDIR([test-libglade.c])
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

dnl get the gnome-common macros
GNOME_COMMON_INIT

AC_ARG_ENABLE(debug, [  --enable-debug             enable debugging output],,
  [enable_debug=no])

if test "x$enable_debug" != xno; then
  AC_DEFINE(DEBUG, [Turn on a bit of debugging spew])
fi

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h, AC_DEFINE(HAVE_UNISTD_H))

AM_PROG_LIBTOOL

AM_PATH_GTK_2_0(1.3.7)

AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

AC_MSG_CHECKING(for libxml2)
if test "x$PKG_CONFIG" != xno && $PKG_CONFIG libxml-2.0; then
    XML_CFLAGS="`$PKG_CONFIG --cflags libxml-2.0`"
    XML_LIBS="`$PKG_CONFIG --libs libxml-2.0`"
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(could not find libxml2)
fi

AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)
AM_CONDITIONAL(HAVE_GTK_DOC, $GTKDOC)

dnl gettext stuff ... there is no message catalog for libglade -- libglade
dnl provides translation for the XML files it reads in.
ALL_LINGUAS=""
AM_GNOME2_GETTEXT
BUILD_INCLUDED_LIBINTL=no
AC_SUBST(BUILD_INCLUDED_LIBINTL)

dnl stuff to go in the config.h file
AH_TOP(
[#ifndef CONFIG_H
#define CONFIG_H])
AH_BOTTOM(
[#ifdef DEBUG
#  define debug(stmnt) stmnt
#else
#  define debug(stmnt) /* nothing */
#endif

#endif])

AC_CONFIG_FILES([Makefile
libglade.spec
intl/Makefile
po/Makefile.in
glade/Makefile
doc/Makefile
libglade-2.0.pc])
AC_OUTPUT

echo "This is the development branch of libglade"
echo "If you want something that works with gtk+ 1.2, checkout the"
echo "libglade-1-0 branch with the following command"
echo "  cvs update -r libglade-1-0 ."
